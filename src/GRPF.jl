__precompile__()

"""
# GRPF: Global complex Roots and Poles Finding algorithm

A Julia implementation of the GRPF [Matlab code](https://github.com/PioKow/GRPF) by Piotr
Kowalczyk. Matlab files (`.m`) listed in _see also_ sections of function doc strings refer
to this git repo.
"""
module GRPF

using LinearAlgebra
using Statistics
using StaticArrays
import GeometricalPredicates: intriangle
using VoronoiDelaunay

include("VoronoiDelaunayExtensions.jl")
include("GeneralFunctions.jl")

export rectangulardomain, diskdomain, grpf

const maxiterations = 100
const maxnodes = 500000
const skinnytriangle = 3


"""
Generate initial mesh node coordinates for a rectangular domain ‚àà {`Zb` to `Ze`}.

See also: `rect_dom.m`
"""
function rectangulardomain(Zb::Complex, Ze::Complex, Œîr)
    X = real(Ze) - real(Zb)
    Y = imag(Ze) - imag(Zb)

    n = ceil(Int64, Y/Œîr + 1)
    dy = Y/(n-1)
    m = ceil(Int64, X/sqrt(Œîr^2 - dy^2/4) + 1)
    dx = X/(m-1)

    vx = range(real(Zb), length=m, stop=real(Ze))
    vy = range(imag(Zb), length=n, stop=imag(Ze))

    tmp = ones(n)
    tmp[n] = 0.0

    y = repeat(vy, m)
    y .+= 0.5*dy*kron((1 .+ (-1).^(1:m))/2, tmp)
    y = [y; fill(imag(Zb), size(2:2:m))]
    x = reshape(repeat(vx', n), m*n)
    x = [x; ((2:2:m) .- 1)*dx .+ real(Zb)]

    # NOTE: Matlab values of `x` differ slightly because of Matlab's float handling and
    # transpose operator.
    # `sum(x)` is much closer between Julia and Matlab if the above line for `x` is:
    # `matlabx = [x' ((2:2:m) .- 1)'*dx .+ real(Zb)]'`

    # # TEMP
    # x = [x' ((2:2:m) .- 1)'*dx .+ real(Zb)]'
    # x = reshape(x, length(x))

    return complex.(x, y)
end

"""
Generate initial mesh coordinates for a circular disk domain of radius `R` and center (0, 0)
for ``|z| < R``.

See also: `disk_dom.m`
"""
function diskdomain(R, Œîr)
    h = Œîr*sqrt(3)/2
    n = 1 + round(Int, R/h)
    Rn = (1:n)*R/n
    newnodes = [complex(0.0)]
    f‚ÇÄ = 0.
    np = 6
    for ii = 1:n
        f = f‚ÇÄ .+ range(0, stop=2œÄ, length=np+1)
        xyn = Rn[ii]*complex.(cos.(f[1:end-1]), sin.(f[1:end-1]))
        append!(newnodes, xyn)
        f‚ÇÄ += œÄ/6/n
        np += 6
    end
    return newnodes
end



"""
Converts complex function value `val` to quadrant number.

See also: `vinq.m`
"""
function quadrant(val::Complex)::UInt8
    if (real(val) > 0) & (imag(val) >= 0)
        quad = 1
    elseif (real(val) <= 0) & (imag(val) > 0)
        quad = 2
    elseif (real(val) < 0) & (imag(val) <= 0)
        quad = 3
    elseif (real(val) >= 0) & (imag(val) < 0)
        quad = 4
    else
        error("Function value $val cannot be assigned to quadrant.")
    end
end

"""
Evaluate function `fcn` for [`quadrant`](@ref) at `nodes` and fill `quadrants`.

`quadrants` is a Vector{} where each index corresponds to `node` index.
"""
function assignquadrants!(quadrants::Vector{UInt8},
                          nodes::Vector{IndexablePoint2D}, fcn::Function)
    for ii in eachindex(nodes)
        val = fcn(nodes[ii])::ComplexF64  # TODO: known return type
        quadrants[getindex(nodes[ii])] = quadrant(val)
    end
    nothing
end

"""
Return candidate edges `ùìî` that contain a phase change of 2 quadrants.

Any root or pole is located at the point where the regions described by four different
quadrants meet. Since any triangulation of the four nodes located in the four different
quadrants requires at least one edge of ``|ŒîQ| = 2``, then all such edges are potentially
in the vicinity of a root or pole.

Notes:
 - Order of `ùìî` is not guaranteed.
 - Count of `phasediffs` of value 1 and 3 can differ from Matlab in normal operation,
 because it depends on "direction" of edge.
 - `phasediffs` is only needed for diagnosis and plotting.
"""
function candidateedges(tess::DelaunayTessellation2D{IndexablePoint2D},
                        quadrants::Vector{UInt8})
    phasediffs = Vector{UInt8}()
    ùìî = Vector{DelaunayEdge{IndexablePoint2D}}()

    edgeiter = delaunayedges(tess)
    for edge in edgeiter
        e::DelaunayEdge{IndexablePoint2D} = edge  # TODO: infer edge type
        nodea, nodeb = geta(e), getb(e)
        idxa, idxb = getindex(nodea), getindex(nodeb)

        # To match Matlab, force `idxa` < `idxb`
        # (order doesn't matter for `ŒîQ == 2`, which is the only case we care about)
        if idxa > idxb
            idxa, idxb = idxb, idxa
        end

        ŒîQ = mod(quadrants[idxa] - quadrants[idxb], 4)
        ŒîQ == 2 && push!(ùìî, e)

        push!(phasediffs, ŒîQ)
    end
    return ùìî, phasediffs
end

"""
Counts how many times each triangle contains a node in `edges`.
"""
function counttriangleswithnodes(tess::DelaunayTessellation2D{IndexablePoint2D}, edges::Vector{DelaunayEdge{IndexablePoint2D}})
    # Nodes of select edges
    edgenodes = Vector{IndexablePoint2D}()
    uniquenodes!(edgenodes, edges)

    trianglecounts = zeros(Int, count(.!isexternal.(tess._trigs)))
    triidx = 0
    for triangle in tess
        triidx += 1
        # `triangle` is in general not equal to `tess._trigs[triidx]`
        ea = geta(triangle)
        eb = getb(triangle)
        ec = getc(triangle)
        for nodeidx in eachindex(edgenodes)
            if (ea == edgenodes[nodeidx]) || (eb == edgenodes[nodeidx]) || (ec == edgenodes[nodeidx])
                trianglecounts[triidx] += 1
            end
        end
    end
    return trianglecounts
end
function uniquenodes!(edgenodes::Vector{IndexablePoint2D}, edges::Vector{DelaunayEdge{IndexablePoint2D}})
    for ii in eachindex(edges)
        nodea = geta(edges[ii])
        nodeb = getb(edges[ii])

        nodea in edgenodes || push!(edgenodes, nodea)
        nodeb in edgenodes || push!(edgenodes, nodeb)
    end
    nothing
end

"""
Add nodes to `newnodes` in zone 1, i.e. triangles that had more than one node.
"""
function zone1newnodes!(newnodes::Vector{IndexablePoint2D},
                        triangles::Vector{DelaunayTriangle{IndexablePoint2D}}, geom2fcn::Function, tolerance)
    triangle1 = triangles[1]
    n1a = geta(triangle1)
    n1b = getb(triangle1)
    push!(newnodes, (n1a+n1b)/2)
    for ii = 1:length(triangles)-1
        na = geta(triangles[ii])
        nb = getb(triangles[ii])
        nc = getc(triangles[ii])

        addnewnode!(newnodes, nb, nc, geom2fcn, tolerance)
        addnewnode!(newnodes, nc, na, geom2fcn, tolerance)
        addnewnode!(newnodes, geta(triangles[ii+1]), getb(triangles[ii+1]), geom2fcn, tolerance)
    end
    na = geta(triangles[end])
    nb = getb(triangles[end])
    nc = getc(triangles[end])
    addnewnode!(newnodes, nb, nc, geom2fcn, tolerance)
    addnewnode!(newnodes, nc, na, geom2fcn, tolerance)

    # Remove the first of `newnodes` if the edge is too short
    distance(geom2fcn(n1a), geom2fcn(n1b)) < tolerance && popfirst!(newnodes)

    nothing
end
function addnewnode!(newnodes::Vector{IndexablePoint2D}, node1::IndexablePoint2D,
                     node2::IndexablePoint2D, geom2fcn::Function, tolerance)
    if distance(geom2fcn(node1), geom2fcn(node2)) > tolerance
        avgnode = (node1+node2)/2
        for ii in eachindex(newnodes)
            distance(newnodes[ii], avgnode) < 2eps() && return nothing
        end
        push!(newnodes, avgnode)  # only executed if we haven't already returned
    end
    nothing
end

"""
Add nodes to `newnodes` in zone 2 (skinny triangles).
"""
function zone2newnodes!(newnodes::Vector{IndexablePoint2D}, triangles::Vector{DelaunayTriangle{IndexablePoint2D}})
    for ii in eachindex(triangles)
        triangle = triangles[ii]::DelaunayTriangle{IndexablePoint2D}
        na = geta(triangle)
        nb = getb(triangle)
        nc = getc(triangle)

        # For skinny triangle check, `geom2fcn` not needed because units cancel out
        l1 = distance(na, nb)
        l2 = distance(nb, nc)
        l3 = distance(nc, na)
        if max(l1,l2,l3)/min(l1,l2,l3) > skinnytriangle
            avgnode = (na+nb+nc)/3
            push!(newnodes, avgnode)
        end
    end
    nothing
end

"""
Find contour edges from all candidate edges.
"""
function contouredges(tess::DelaunayTessellation2D{IndexablePoint2D}, edges::Vector{DelaunayEdge{IndexablePoint2D}})
    # Edges of triangles that contain at least 1 of `edges`
    tmpedges = Vector{DelaunayEdge{IndexablePoint2D}}()
    for triangle in tess
        # We don't know which "direction" the edges are defined in the triangle,
        # so we need to test both
        edgea = DelaunayEdge(geta(triangle), getb(triangle))
        edgearev = DelaunayEdge(getb(triangle), geta(triangle))
        edgeb = DelaunayEdge(getb(triangle), getc(triangle))
        edgebrev = DelaunayEdge(getc(triangle), getb(triangle))
        edgec = DelaunayEdge(getc(triangle), geta(triangle))
        edgecrev = DelaunayEdge(geta(triangle), getc(triangle))

        # Does triangle contain edge?
        for edge in edges
            if edgea == edge || edgeb == edge || edgec == edge ||
                edgearev == edge || edgebrev == edge || edgecrev == edge
                push!(tmpedges, edgea, edgeb, edgec)
                break  # only count each triangle once
            end
        end
    end

    # Remove duplicate (reverse) edges from `tmpedges` and otherwise append to `ùê∂`
    ùê∂ = Vector{DelaunayEdge{IndexablePoint2D}}()
    duplicateedges = zeros(Int, length(tmpedges))
    for (idxa, edgea) in enumerate(tmpedges)
        if duplicateedges[idxa] == 0
            for (idxb, edgeb) in enumerate(tmpedges)
                # Check if Edge(a,b) == Edge(b, a), i.e. there are duplicate edges
                if edgea == DelaunayEdge(getb(edgeb), geta(edgeb))
                    duplicateedges[idxa] = 2
                    duplicateedges[idxb] = 2
                    break
                end
            end
            if duplicateedges[idxa] != 2
                duplicateedges[idxa] = 1
                push!(ùê∂, edgea)
            end
        end
    end

    return ùê∂
end

"""
Separate triangles by zones.
"""
function splittriangles(tess::DelaunayTessellation2D{IndexablePoint2D}, trianglecounts::Vector{Int})
    zone1triangles = Vector{DelaunayTriangle{IndexablePoint2D}}()
    zone2triangles = Vector{DelaunayTriangle{IndexablePoint2D}}()
    ii = 0
    for triangle in tess
        ii += 1
        if trianglecounts[ii] > 1
            push!(zone1triangles, triangle)
        elseif trianglecounts[ii] == 1
            push!(zone2triangles, triangle)
        end
    end
    return zone1triangles, zone2triangles
end

"""
Find the index of the next node in the candidate region boundary process. The next one (after
the reference) is picked from the fixed set of nodes.

See also: `FindNextNode.m`
"""
function findnextnode(prevnode::IndexablePoint2D, refnode::IndexablePoint2D,
                      tempnodes::Vector{IndexablePoint2D}, geom2fcn::Function)
    P = geom2fcn(prevnode)
    S = geom2fcn(refnode)
    N = geom2fcn.(tempnodes)

    numtempnodes = length(N)

    SP = ones(numtempnodes)*(P-S)
    SN = N .- S

    SPlength = norm.(SP)
    SNlength = norm.(SN)

    dotprod = real.(SP).*real.(SN)+imag.(SP).*imag.(SN)
    œï = acos.(dotprod./(SPlength.*SNlength))
    tmp = findall(real.(SP).*imag.(SN)-imag.(SP).*real.(SN) .< 0)
    œï[tmp] .= 2œÄ .- œï[tmp]
    findmin(œï)[2]  # return index of minimum `œï`
end

"""
TODO: Go in reverse (from `end` rather than `1` so we don't popfirst!) or use careful indexing
and don't pop at all

# Note: The nodes of each region are in reverse order compared to Matlab wrt their quadrants?
"""
function evaluateregions!(ùê∂::Vector{DelaunayEdge{IndexablePoint2D}}, geom2fcn::Function)
    # Initialize
    numregions = 1

    regions = [[geta(ùê∂[1])]]
    refnode = getb(ùê∂[1])
    popfirst!(ùê∂)
    while length(ùê∂) > 0
        nextedgeidx = findall(e->geta(e)==refnode, ùê∂)

        if isempty(nextedgeidx)
            push!(regions[numregions], refnode)
            # New region
            numregions += 1
            push!(regions, [geta(ùê∂[1])])
            refnode = getb(ùê∂[1])
            popfirst!(ùê∂)
        else
            if length(nextedgeidx) > 1
                prevnode = regions[numregions][end]
                tempnodes = getb.(ùê∂[nextedgeidx])
                idx = findnextnode(prevnode, refnode, tempnodes, geom2fcn)
                nextedgeidx = nextedgeidx[idx]
            else
                nextedgeidx = nextedgeidx[1]
            end

            nextedge = ùê∂[nextedgeidx]
            push!(regions[numregions], geta(nextedge))
            refnode = getb(nextedge)
            deleteat!(ùê∂, nextedgeidx)
        end
    end
    push!(regions[numregions], refnode)

    return regions
end

"""
"""
function rootsandpoles(regions::Vector{Vector{IndexablePoint2D}}, quadrants::Vector{UInt8}, geom2fcn::Function)
    numregions = size(regions, 1)
    q = Vector{Union{Missing, Int}}(undef, numregions)
    z = Vector{ComplexF64}(undef, numregions)
    for ii in eachindex(regions)
        # XXX: ORDER OF REGIONS??? XXX
        quadrantsequence = [convert(Int8, quadrants[getindex(node)]) for node in regions[ii]]
        # Sign flip because `regions[ii]` are in opposite order of Matlab??
        dQ = -diff(quadrantsequence)
        for jj in eachindex(dQ)
            dQ[jj] == 3 && (dQ[jj] = -1)
            dQ[jj] == -3 && (dQ[jj] = 1)
            # ``|ŒîQ| = 2`` is ambiguous; cannot tell whether phase increases or decreases by two quadrants
            abs(dQ[jj]) == 2 && (dQ[jj] = 0)
        end
        q[ii] = sum(dQ)/4
        z[ii] = mean(geom2fcn.(regions[ii]))
    end
    zroots = [z[i] for i in eachindex(z) if q[i] > 0]
    zroots_multiplicity = filter(x->x>0, q)

    zpoles = [z[i] for i in eachindex(z) if q[i] < 0]
    zpoles_multiplicity = filter(x->x<0, q)

    return zroots, zroots_multiplicity, zpoles, zpoles_multiplicity
end

"""
"""
function tesselate!(tess::DelaunayTessellation2D{IndexablePoint2D}, newnodes::Vector{IndexablePoint2D},
                    fcn::Function, geom2fcn::Function, tolerance)
    # Initialize
    numnodes = tess._total_points_added
    @assert numnodes == 0

    ùìî = Vector{DelaunayEdge{IndexablePoint2D}}()
    quadrants = Vector{UInt8}()

    iteration = 0
    while (iteration < maxiterations) & (numnodes < maxnodes)
        iteration += 1

        # Determine which quadrant function value belongs at each node
        numnewnodes = length(newnodes)
        append!(quadrants, Vector{UInt8}(undef, numnewnodes))
        assignquadrants!(quadrants, newnodes, fcn)

        # Add new nodes to `tess`
        push!(tess, newnodes)
        numnodes += numnewnodes

        # Determine candidate edges that may be near a root or pole
        ùìî, phasediffs = candidateedges(tess, quadrants)
        isempty(ùìî) && error("No roots in the domain")

        # Select candidate edges that are longer than the chosen tolerance
        selectùìî = filter(e -> longedge(e, tolerance, geom2fcn), ùìî)
        isempty(selectùìî) && return tess, ùìî, quadrants, phasediffs
        maxùìîlength = maximum(distance(p1, p2) for (p1, p2) in geom2fcn.(selectùìî))
        @debug "Candidate edges length max: $maxùìîlength"
        maxùìîlength < tolerance && return tess, ùìî, quadrants, phasediffs

        # How many times does each triangle contain a `selectùìî` node?
        trianglecounts = counttriangleswithnodes(tess, selectùìî)
        zone1triangles, zone2triangles = splittriangles(tess, trianglecounts)

        # Add new nodes in zone 1
        newnodes = Vector{IndexablePoint2D}()
        zone1newnodes!(newnodes, zone1triangles, geom2fcn, tolerance)

        # Add new nodes in zone 2
        zone2newnodes!(newnodes, zone2triangles)

        # Have to assign indexes to new nodes (which are all currently -1)
        setindex!.(newnodes, (1:length(newnodes)).+numnodes)
    end

    return tess, ùìî, quadrants, phasediffs
end

"""
"""
function grpf(tess::DelaunayTessellation2D{IndexablePoint2D}, newnodes::Vector{IndexablePoint2D},
              fcn::Function, geom2fcn::Function, tolerance)
    tess, ùìî, quadrants = tesselate!(tess, newnodes, fcn, geom2fcn, tolerance)
    ùê∂ = contouredges(tess, ùìî)
    regions = evaluateregions!(ùê∂, geom2fcn)
    zroots, zroots_multiplicity, zpoles, zpoles_multiplicity = rootsandpoles(regions, quadrants, geom2fcn)

    return zroots, zroots_multiplicity, zpoles, zpoles_multiplicity
end

end # module
